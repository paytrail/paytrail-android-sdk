import org.jetbrains.kotlin.gradle.tasks.KotlinCompile


buildscript {
    repositories {
        maven { url "https://repo1.maven.org/maven2" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath libs.kotlin.gradle.plugin
        classpath libs.kotlin.serialization
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'


ktlint {
    version.set(libs.versions.ktlint)
    outputToConsole.set(true)
    verbose.set(true)
}

test {
    useJUnitPlatform()
}

dependencies {

    implementation libs.kotlin.stdlib.jdk8
    implementation libs.kotlin.reflect
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.serialization.json
    api libs.okhttp3.client
    api libs.okhttp3.logging.interceptor
    api libs.retrofit
    implementation libs.retrofit2.kotlinx.serialization.converter
    implementation libs.retrofit.converter.scalars
    testImplementation libs.kotlintest.runner.junit5
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
    }
}

publishing {
    publications {
        apiClientRetrofit2(MavenPublication) {
            from components.kotlin
            groupId "com.github.paytrail"
            artifactId "api-client-retrofit2"
            version "v0.1.0-alpha"
        }
    }
}
